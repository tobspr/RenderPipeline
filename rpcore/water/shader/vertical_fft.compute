#version 430

layout (local_size_x = 16, local_size_y = 16) in;
uniform sampler2D source;
uniform sampler2D precomputedWeights;
uniform writeonly image2D dest;
uniform int butterflyIndex;
uniform int N;
uniform float normalizationFactor;
uniform bool isLastPass;

void main() {
    ivec2 texelCoords = ivec2(gl_GlobalInvocationID.xy);
    vec4 weights = texelFetch(precomputedWeights, ivec2(texelCoords.y, butterflyIndex), 0);
    int sampleIndexA = int(weights.x * N + 0.5);
    int sampleIndexB = int(weights.y * N + 0.5);
    vec2 sampledValA = texelFetch(source, ivec2(texelCoords.x, sampleIndexA), 0).xy;
    vec2 sampledValB = texelFetch(source, ivec2(texelCoords.x, sampleIndexB), 0).xy;
    
    vec2 weight = weights.zw * 2.0 - 1.0;
    vec2 weightedValB = weight * sampledValB.r + weight.gr * sampledValB.g * vec2(-1,1);
    vec2 result = sampledValA + weightedValB;

    if (!isLastPass) {
        imageStore(dest, texelCoords, vec4(result, 0, 1));
    } else {
        if ( (texelCoords.x + texelCoords.y) % 2 == 1) {
            result.x *= -1;
        }
        float normalizedResult = result.x / normalizationFactor + 0.5;
        imageStore(dest, texelCoords, vec4(normalizedResult, normalizedResult, normalizedResult, 1));
    }
}